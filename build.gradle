plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group 'net.portswigger.burp.extensions.example'
version '1.0.2'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        force 'com.google.protobuf:protobuf-java:4.29.2'
    }
}

dependencies {
    compileOnly 'net.portswigger.burp.extensions:montoya-api:2025.11'
    implementation 'org.mozilla:rhino:1.7.14.1'
    implementation 'org.json:json:20250517'
    implementation 'com.fifesoft:rsyntaxtextarea:3.6.0'
    implementation 'org.python:jython-standalone:2.7.4'
    implementation 'io.grpc:grpc-netty-shaded:1.70.0'
    implementation ('io.grpc:grpc-protobuf:1.70.0') {
        exclude group: 'com.google.protobuf'
    }
    implementation ('io.grpc:grpc-stub:1.70.0') {
        exclude group: 'com.google.protobuf'
    }
    // Force specific protobuf version compatible with gRPC and Burp Suite
    implementation 'com.google.protobuf:protobuf-java:4.29.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.51.1.0'
    implementation "com.github.kklisura.cdt:cdt-java-client:4.0.0"
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    
    // Relocate (shade) protobuf and grpc to avoid conflicts with Burp Suite
    relocate 'com.google.protobuf', 'io.github.cyal1.pyburp.shaded.protobuf'
    relocate 'io.grpc', 'io.github.cyal1.pyburp.shaded.grpc'
    relocate 'com.google.common', 'io.github.cyal1.pyburp.shaded.guava'
    relocate 'io.perfmark', 'io.github.cyal1.pyburp.shaded.perfmark'
    relocate 'com.google.gson', 'io.github.cyal1.pyburp.shaded.gson'
    relocate 'javax.annotation', 'io.github.cyal1.pyburp.shaded.javax.annotation'
    
    exclude '**/org/sqlite/native/FreeBSD/**'
    exclude '**/org/sqlite/native/Linux-Android/**'
    exclude '**/org/sqlite/native/Linux-Musl/**'
    exclude '**/org/sqlite/native/Linux/ppc64/**'
    exclude '**/org/sqlite/native/Linux/riscv64/**'
    exclude '**/org/sqlite/native/Windows/x86/**'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

jar {
    // Empty jar task configuration as shadowJar handles the fat jar creation
    manifest {
        attributes 'Main-Class': 'io.github.cyal1.pyburp.BurpExtender'
    }
}

build.dependsOn shadowJar

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17